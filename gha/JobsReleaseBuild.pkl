// ==-----------------------------------------------------------== //
// SPDX-FileCopyrightText: Â© 2025 Nayan Patil <nayantsg@proton.me>
//
// SPDX-License-Identifier: Apache-2.0
// ==-----------------------------------------------------------== //

@ModuleInfo { minPklVersion = "0.27.0" }
module JobsReleaseBuild

import "../schemas/GitHubAction.pkl" as gha

import "ActionsRegistry.pkl" as ActReg
import "StepsCommon.pkl"
import "UtilsCommon.pkl"

const function reuseSPDXSBOM(ref: String, archivePrefix: String?, archiveTag: String) = new gha.Job {
  name = "Generate REUSE SPDX SBOM"
  steps {
    StepsCommon.checkoutWithRef(0, ref)
    new {
      name = "REUSE SPDX SBOM"
      uses = ActReg.reuseAction
      with {
        ["args"] = "spdx -o reuse.spdx"
      }
    }
    new {
      name =  "Determine archive name"
      run = """
      echo "ARCHIVE=sbom_\(archiveTag)" >> $GITHUB_ENV
      """
      shell = "bash"
    }
    new {
      name = "Create directory for archive and transfer files"
      run = """
      mkdir -p "$ARCHIVE"
      cp reuse.spdx "$ARCHIVE"/
      """
      shell = "bash"
    }
    new {
      name = "Build archive"
      run = """
      tar czf "$ARCHIVE.tar.gz" "$ARCHIVE"
      shasum -a 256 "$ARCHIVE.tar.gz" > "$ARCHIVE.tar.gz.sha256"
      echo "ASSET=$ARCHIVE.tar.gz" >> $GITHUB_ENV
      echo "ASSET_SUM=$ARCHIVE.tar.gz.sha256" >> $GITHUB_ENV
      """
      shell = "bash"
    }
    new {
      name = "Upload tars and sums"
      uses = ActReg.uploadArtifact
      with {
        ["name"] = if (archivePrefix == null) "sbom_\(archiveTag)" else "sbom_\(archivePrefix)-\(archiveTag)"
        ["overwrite"] = true
        ["compression-level"] = 9
        ["path"] = """
        ${{ env.ASSET }}
        ${{ env.ASSET_SUM }}
        """
      }
    }
  }
} |> UtilsCommon.standardJobConfig(null, null)
