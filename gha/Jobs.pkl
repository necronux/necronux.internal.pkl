// ==-----------------------------------------------------------== //
// SPDX-FileCopyrightText: Â© 2025 Nayan Patil <nayantsg@proton.me>
//
// SPDX-License-Identifier: Apache-2.0
// ==-----------------------------------------------------------== //

@ModuleInfo { minPklVersion = "0.27.0" }
module Jobs

import "../schemas/GitHubAction.pkl" as gha

import "Common.pkl"

// Conclusion Jobs:
// To prevent this job from being skipped if its dependencies fail,
// we need to override the if: condition. A skipped job is treated as a success by GitHub,
// which we want to avoid. By using !cancelled(), we ensure the job won't run
// if the workflow is manually canceled.

const function conclusionAll() = new gha.Job {
  name = "Conclusion"
  `if` = "${{ !cancelled() }}"
  permissions = (Common.contentPermissions("none")) {}
  steps {
    // Manually check the status of all dependencies. `if: failure()` does not work.
    new {
      name = "Conclusion"
      run = """
        jq -C <<< '${{ toJson(needs) }}'
        jq --exit-status 'all(.result == "success")' <<< '${{ toJson(needs) }}'
        """
      shell = "bash"
    }
  }
} |> Common.standardJobConfig(null, null)

const function conclusionPR() = (conclusionAll()) {
  name = "Conclusion PR"
  `if` = Common.ifDevelopOrPrOrWDPrAndNotCancelled
}

const function conclusionPush() = (conclusionAll()) {
  name = "Conclusion Push"
  `if` = Common.ifDevelopOrPushOrWDPushAndNotCancelled
}
